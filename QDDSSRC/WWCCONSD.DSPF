     A*%%TS  SD  20070302  205012  ROYMAN      REL-V5R4M0  5722-WDS
     A*‚---------------------------------------------------------------
     A*ˆCOPYRIGHT DATABOROUGH LTD 2005
     A*ˆWork with Contacts
     A*‚---------------------------------------------------------------
     A*‚
     A*‚---------------------------------------------------------------
     A*‚File level definitions
     A*‚---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      VLDCMDKEY(28 'Valid Command Key')
     A*‚---------------------------------------------------------------
     A*‚Subfile Format
     A*‚---------------------------------------------------------------
     A          R ZZSF01                    SFL
     A            DSSEL          2A  B 10  2
     A            DSPRPCDE       2A  O 10  7
     A            DSUSERNM      34A  O 10 13
     A            DSTELNO       17A  O 10 48
     A*‚---------------------------------------------------------------
     A*‚Subfile Control Format
     A*‚---------------------------------------------------------------
     A          R ZZCT01                    SFLCTL(ZZSF01)
     A                                      TEXT('Sub-file control format')
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  31                                  SFLDSP
     A                                      SFLDSPCTL
     A  30                                  SFLDLT
     A  31                                  SFLEND
     A                                      BLINK
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A            DSPREC         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'Customers'
     A                                      COLOR(BLU)
     A                                  1 23'Work with Customer Contacts'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10      2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE        10A  O  3 70OVRDTA
     A                                  4  2'Customer No:'
     A                                      DSPATR(HI)
     A            DSCUSNO        5Y 0O  4 15
     A                                  5  2'Enter options, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'5=Display'
     A                                      COLOR(BLU)
     A                                  8  7'Prod.'
     A                                  8 13'Name'
     A                                  8 48'Tel. No.'
     A*‚---------------------------------------------------------------
     A*‚Trailer Format
     A*‚---------------------------------------------------------------
     A          R ZZTR01
     A*%%TS  SD  20070302  205012  ROYMAN      REL-V5R4M0  5722-WDS
     A                                      PUTOVR
     A                                      OVERLAY
     A                                      BLINK
     A                                      KEEP
     A                                 23  2'F1=Help'
     A                                      COLOR(BLU)
     A                                 23 11'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Cancel'
     A                                      COLOR(BLU)
     A*‚---------------------------------------------------------------
     A*‚Flat Screen Enquiry Format
     A*‚---------------------------------------------------------------
     A          R ZZFT01
     A                                      TEXT('Enquiry format')
     A                                      OVERLAY
     A                                      KEEP
     A                                      PUTOVR
     A                                      BLINK
     A                                  1  2'Customers'
     A                                      COLOR(BLU)
     A                                  1 23'Work with Customer Contacts'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10      2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE        10A  O  3 70OVRDTA
     A                                  4  3'Customer No  . . . . .'
     A            DSCUSNO        5Y 0O  4 29
     A                                      OVRDTA
     A                                  5  3'Product Code . . . . .'
     A            DSPRPCDE       2A  O  5 29
     A                                      OVRDTA
     A                                  6  3'Contact  . . . . . . . .'
     A            DSUSERNM      34A  O  6 29
     A                                      OVRDTA
     A                                  7  3'Telephone No . . . . . .'
     A            DSTELNO       17A  O  7 29
     A                                      OVRDTA
     A                                  8  3'Fax No . . . . . . . . .'
     A            DSFAXNO       15A  O  8 29
     A                                      OVRDTA
     A                                  8  3'E-Mail . . . . . . . . .'
     A            DSEMAIL       50A  O  9 29
     A                                      OVRDTA
     A                                 10  3'Last Contact Date  . . .'
     A            DSLCTDAT      10A  O 10 29
     A                                      OVRDTA
     A                                 11  3'Next Contact Date  . . .'
     A            DSAPDATE      10A  O 11 29
     A                                      OVRDTA
     A                                 12  3'Salesperson  . . . . . .'
     A            DSSINIT        3A  O 12 29
     A                                      OVRDTA
     A                                 13  3'Status . . . . . . . . .'
     A            DSSTATUS       1A  O 13 29
     A                                      OVRDTA
     A                                 23  2'F1=Help'
     A                                      COLOR(BLU)
     A                                 23 11'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Cancel'
     A                                      COLOR(BLU)
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Format
     A*‚---------------------------------------------------------------
     A          R ZZMS01                    SFL
     A                                      TEXT('Message sub-file')
     A                                      SFLMSGRCD(24)
     A            ZZMSKY                    SFLMSGKEY
     A            ZZPGM                     SFLPGMQ
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Control Format
     A*‚---------------------------------------------------------------
     A          R ZZMC01                    SFLCTL(ZZMS01)
     A                                      TEXT('Message control')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLPAG(1)
     A                                      SFLSIZ(2)
     A  32                                  SFLINZ
     A  32                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  32                                  SFLEND
     A            ZZPGM                     SFLPGMQ
