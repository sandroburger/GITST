     A*‚---------------------------------------------------------------
     A*ˆCOPYRIGHT DATABOROUGH LTD 2011
     A*ˆWork with Item Master Records
     A*‚---------------------------------------------------------------
     A*‚
     A*‚---------------------------------------------------------------
     A*‚File level definitions
     A*‚---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      HELP
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      VLDCMDKEY(28 'Valid Command Key')
     A*‚---------------------------------------------------------------
     A*‚Subfile Format
     A*‚---------------------------------------------------------------
     A          R ZZSF01                    SFL
     A            DSSEL          1A  B 10  2
     A            #ITEM         10A  O 10  5TEXT('ITEM #')
     A            #DESC         30A  O 10 20TEXT('DESCRIPTION')
     A            #PART         15A  O 10 60TEXT('PART #')
     A*‚---------------------------------------------------------------
     A*‚Subfile Control Format
     A*‚---------------------------------------------------------------
     A          R ZZCT01                    SFLCTL(ZZSF01)
     A                                      TEXT('Sub-file control format')
     A                                      CA06(06 'Add')
     A N81                                  PAGEDOWN(26)
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A  71                                  SFLDSP
     A  72                                  SFLDSPCTL
     A  74                                  SFLCLR
     A  81                                  SFLEND(*MORE)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A            SHWREC         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1 31'Work with Items'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10A  O  2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE         8Y 0O  3 70EDTWRD('    /  /  ')
     A                                  4  2'Position to:'
     A                                  5  2'Enter options, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'2=Change, 4=Delete, 5=Display'
     A                                      COLOR(BLU)
     A                                  8 17'                  Name            -
     A                                            '
     A                                      DSPATR(UL)
     A                                  8  5'Item #    '
     A                                      DSPATR(UL)
     A                                  8 20'Description                   '
     A                                      DSPATR(UL)
     A                                  8 60'Part #    '
     A                                      DSPATR(UL)
     A            LOCATE        11A  B  4 15
     A*‚---------------------------------------------------------------
     A*‚Trailer Format
     A*‚---------------------------------------------------------------
     A          R ZZTR01
     A                                      OVERLAY
     A                                      BLINK
     A                                      KEEP
     A            ZMSAGE        78A  O 24  2DSPATR(HI)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 11'F6=Add'
     A                                      COLOR(BLU)
     A                                 23 19'F12=Cancel'
     A                                      COLOR(BLU)
     A*‚---------------------------------------------------------------
     A*‚Flat Screen Maintenance Format 1
     A*‚---------------------------------------------------------------
     A          R ZZFT01
     A                                      TEXT('Enquiry format')
     A N94                                  CA04(04 'PROMPT')
     A                                      KEEP
     A                                      RTNCSRLOC(&RECNAM &SFIELD)
     A                                      BLINK
     A                                      OVERLAY
     A            SFIELD        10A  H
     A            RECNAM        10A  H
     A                                  1 33'Work with Items'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10A  O  2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE         8Y 0O  3 70EDTWRD('    /  /  ')
     A            ACTDSP        10A  O  4  2DSPATR(RI)
     A                                  5  2'Item #  . . . . . .'
     A            ZITEM         10A  B  5 22
     A  31                                  DSPATR(RI)
     A N96                                  DSPATR(PR)
     A                                  6  2'Description . . . .'
     A            ZDESC         30A  B  6 22
     A  32                                  DSPATR(RI)
     A  94                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  7  2'Part #  . . . . . .'
     A            ZPART         15A  B  7 22
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 11'F4=Prompt'
     A                                      COLOR(BLU)
     A  94                                  DSPATR(ND)
     A                                 23 51'F12=Cancel'
     A                                      COLOR(BLU)
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Format
     A*‚---------------------------------------------------------------
     A          R ZZMS01                    SFL
     A                                      TEXT('Message sub-file')
     A                                      SFLMSGRCD(24)
     A            ZZMSKY                    SFLMSGKEY
     A            ZZPGM                     SFLPGMQ
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Control Format
     A*‚---------------------------------------------------------------
     A          R ZZMC01                    SFLCTL(ZZMS01)
     A                                      TEXT('Message control')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLPAG(1)
     A                                      SFLSIZ(2)
     A  82                                  SFLINZ
     A  82                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  82                                  SFLEND
     A            ZZPGM                     SFLPGMQ
     A*‚---------------------------------------------------------------
     A          R DUMMY
     A                                      KEEP
     A*                                     ASSUME
     A                                      OVERLAY
     A                                  1  3' '
     A*‚---------------------------------------------------------------
     A          R ZZCNF1
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A                                  1 33'Work with Items'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10A  O  2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE         8Y 0O  3 70EDTWRD('    /  /  ')
     A                                  4  2'  CONFIRMATION  '
     A                                      DSPATR(RI)
     A                                  6  2'Item # . . . . . . '
     A            ZITEM         10A  O  6 22
     A                                  7  2'Description . . . .'
     A            ZDESC         30A  O  7 22
     A                                  8  2'Part # .  . . . . .'
     A            ZPART     R        O  8 22
     A            ZCMDF         78A  O 23  2COLOR(BLU)
     A                                      TEXT('Function Key Usage')
     A                                 22 30'Press ENTER to confirm'
     A                                      DSPATR(RI)
