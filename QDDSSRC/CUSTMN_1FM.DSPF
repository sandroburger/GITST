     A*‚---------------------------------------------------------------
     A*ˆCOPYRIGHT DATABOROUGH LTD 2005
     A*ˆWork with Rep. Delivery Areas
     A*‚---------------------------------------------------------------
     A*‚
     A*‚---------------------------------------------------------------
     A*‚File level definitions
     A*‚---------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      HELP
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      VLDCMDKEY(28 'Valid Command Key')
     A*‚---------------------------------------------------------------
     A*‚Flat Screen Maintenance Format 1
     A*‚---------------------------------------------------------------
     A          R ZZFT01
     A                                      TEXT('Enquiry format')
     A N94                                  CA04(04 'PROMPT')
     A                                      KEEP
     A                                      RTNCSRLOC(&RECNAM &SFIELD)
     A                                      BLINK
     A                                      OVERLAY
     A            SFIELD        10A  H
     A            RECNAM        10A  H
     A                                  1  2'Customers'
     A                                      COLOR(BLU)
     A                                  1 28'Customer Maintenance'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10A  O  2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE         8Y 0O  3 70EDTWRD('    /  /  ')
     A                                  5  2'Customer No . . . .'
     A            SWBCCD        11A  B  5 22
     A  31                                  DSPATR(RI)
     A N96                                  DSPATR(PR)
     A                                  6  2'Customer Name . . .'
     A            SWG4TX        40A  B  6 22
     A  32                                  DSPATR(RI)
     A  94                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  7  2'Statement Account .'
     A            SWB2CD        11A  B  7 22
     A  94                                  DSPATR(PR)
     A                                  8  2'Related Account . .'
     A            SWB3CD        11A  B  8 22
     A  94                                  DSPATR(PR)
     A                                  9  2'Tax Reg . . . . . .'
     A            SWHITX        15A  B  9 22
     A  94                                  DSPATR(PR)
     A                                 10  2'Bank. . . . . . . .'
     A            SWE0NB    R        B 10 22REFFLD(XWE0NB XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                      RANGE(0 999999999)
     A                                 11  2'Bank Account. . . .'
     A            SWJUN0    R        B 11 22REFFLD(XWJUN0 XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 11 44'Forex . . . . . .'
     A            SWDVCD         3A  B 11 62
     A  94                                  DSPATR(PR)
     A                                 12  2'CusGrp. . . . . . >'
     A            SWBNCD         2A  B 12 22
     A  94                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A                                 13  2'Rep . . . . . . . >'
     A            SPERSON        3A  B 13 22
     A  94                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A            PNAME     R        O 13 26REFFLD(PNAME XAN4CDEM/SLMEN)
     A                                      DSPATR(HI)
     A                                 14  2'Distributor . . . >'
     A            SDSDCDE        2A  B 14 22
     A  94                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A                                 15 44'Terms . . . . . .'
     A            SWBTCD         3A  B 15 62
     A  94                                  DSPATR(PR)
     A                                 15  2'Credit Limit. . . .'
     A            SWGIVA    R        B 15 22REFFLD(XWGIVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 16  2'Stl Dsc . . . . . .'
     A            SWAOCD         3A  B 16 22
     A  94                                  DSPATR(PR)
     A                                 16 44'Int . . . . . . .'
     A            SWBBCD         3A  B 16 62
     A  94                                  DSPATR(PR)
     A                                 17  2'Cr Guarantee. . . .'
     A            SWG4T0        10A  B 17 22
     A  94                                  DSPATR(PR)
     A                                 18  2'B/O . . . . . . . .'
     A            SWC7ST         1A  B 18 22VALUES(' ' 'Y' 'N')
     A  94                                  DSPATR(PR)
     A                                 18 44'Lang. . . . . . .'
     A            SWDAST         1A  B 18 62
     A  94                                  DSPATR(PR)
     A                                 19  2'Date Loaded . . . .'
     A            SWBPD0          L  B 19 22
     A  94                                  DSPATR(PR)
     A                                 19 44'Chg-Date. . . . .'
     A            SWBAD0          L  B 19 62
     A  94                                  DSPATR(PR)
     A                                 20  2'Last Sale . . . . .'
     A            SWARDT          L  B 20 22
     A  94                                  DSPATR(PR)
     A            ZMSAGE        78A  O 24  2DSPATR(HI)
     A            SWKHTX        40A  O 12 26DSPATR(HI)
     A                                      TEXT('Full Name')
     A            DNAME         34A  O 14 26DSPATR(HI)
     A                                      TEXT('Full Name')
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 41'F4=Prompt'
     A                                      COLOR(BLU)
     A  94                                  DSPATR(ND)
     A                                 23 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*‚---------------------------------------------------------------
     A*‚Flat Screen Maintenance Format 2
     A*‚---------------------------------------------------------------
     A          R ZZFT02
     A N94                                  CA04(04 'PROMPT')
     A                                      KEEP
     A                                      RTNCSRLOC(&RECNAM &SFIELD)
     A                                      BLINK
     A                                      OVERLAY
     A            SFIELD        10A  H
     A            RECNAM        10A  H
     A                                  1  2'Customers'
     A                                      COLOR(BLU)
     A                                  1 28'Customer Detail Maintenance'
     A                                      DSPATR(HI)
     A                                  1 64'Databorough Ltd.'
     A                                      COLOR(BLU)
     A            ZZPGM         10A  O  2  2TEXT('Program Name')
     A                                  2 72TIME
     A            ZZDATE         8Y 0O  3 70EDTWRD('    /  /  ')
     A                                  6  2'Amt Last Sale . . .'
     A            SWF0VA    R        B  6 22REFFLD(XWF0VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                  7  2'Date Last Pay . . .'
     A            SWASDT          L  B  7 22
     A  94                                  DSPATR(PR)
     A                                  8  2'Last Pay. . . . . .'
     A            SWF0V0    R        B  8 22REFFLD(XWF0V0 XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                  9  2'Date Last Stmt. . .'
     A            SWBQDT          L  B  9 22
     A  94                                  DSPATR(PR)
     A                                 10  2'Curr Pay. . . . . .'
     A            SWF1VA    R        B 10 22REFFLD(XWF1VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 11  2'Curr Claim. . . . .'
     A            SWF1V0    R        B 11 22REFFLD(XWF1V0 XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 12  2'Curr Sls. . . . . .'
     A            SWF2VA    R        B 12 22REFFLD(XWF2VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 12 44'Curr Cost . . . .'
     A            SWF3VA    R        B 12 62REFFLD(XWF3VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 13  2'Ytd Sales . . . . .'
     A            SWF4VA    R        B 13 22REFFLD(XWF4VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 13 44'Last Year Sls . .'
     A            SWF6VA    R        B 13 62REFFLD(XWF6VA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 14  2'Balance . . . . . .'
     A            SWIDV0    R        B 14 22REFFLD(XWIDV0 XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A                                 15  2'Current Amt . . . .'
     A            SWGAVA    R        B 15 22REFFLD(XWGAVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 16  2'030 Days. . . . . .'
     A            SWGBVA    R        B 16 22REFFLD(XWGBVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 16 44'060 Days. . . . .'
     A            SWGCVA    R        B 16 62REFFLD(XWGCVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 17  2'090 Days. . . . . .'
     A            SWGDVA    R        B 17 22REFFLD(XWGDVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 17 44'120 Days. . . . .'
     A            SWGEVA    R        B 17 62REFFLD(XWGEVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 18  2'150 Days. . . . . .'
     A            SWGFVA    R        B 18 22REFFLD(XWGFVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 18 44'180 Days. . . . .'
     A            SWGGVA    R        B 18 62REFFLD(XWGGVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 19  2'Total Days. . . . .'
     A            SWTOTA    R        B 19 22REFFLD(XWGGVA XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A                                 20  2'Site no . . . . . >'
     A            CUSNO     R        B 20 22REFFLD(CUSNO XAN4CDEM/CUSTS)
     A  94                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      RANGE(0 99999)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 27'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 40'F4=Prompt'
     A                                      COLOR(BLU)
     A  94                                  DSPATR(ND)
     A            ZMSAGE        78A  O 24  2DSPATR(HI)
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Format
     A*‚---------------------------------------------------------------
     A          R ZZMS01                    SFL
     A                                      TEXT('Message sub-file')
     A                                      SFLMSGRCD(24)
     A            ZZMSKY                    SFLMSGKEY
     A            ZZPGM                     SFLPGMQ
     A*‚---------------------------------------------------------------
     A*‚Message Subfile Control Format
     A*‚---------------------------------------------------------------
     A          R ZZMC01                    SFLCTL(ZZMS01)
     A                                      TEXT('Message control')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLPAG(1)
     A                                      SFLSIZ(2)
     A  82                                  SFLINZ
     A  82                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  82                                  SFLEND
     A            ZZPGM                     SFLPGMQ
     A*‚---------------------------------------------------------------
     A          R DUMMY
     A*%%TS  SD  20070126  201426  ROYMAN      REL-V5R3M0  5722-WDS
     A                                      KEEP
     A*                                     ASSUME
     A                                      OVERLAY
     A                                  1  3' '
